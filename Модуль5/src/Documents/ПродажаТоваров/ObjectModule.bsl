
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Товары.Очистить();
	Движения.Товары.Записывать = Истина;
	Движения.Товары.БлокироватьДляИзменения = Истина;
	
	Для Каждого ТекСтрокаТовары из Товары Цикл
		Движение = Движения.Товары.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Товар = ТекСтрокаТовары.Товар;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;
	
	Движения.Записать();
	
	ЗапросОстатков = Новый Запрос(
	"ВЫБРАТЬ
	|	ТоварыОстатки.Товар,
	|	ТоварыОстатки.Товар.Представление,
	|	ТоварыОстатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.Товары.Остатки(&МоментВремени, Товар В
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ПродажаТоваровТовары.Товар
	|		ИЗ
	|			Документ.ПродажаТоваров.Товары КАК ПродажаТоваровТовары
	|		ГДЕ
	|			ПродажаТоваровТовары.Ссылка = &Ссылка)) КАК ТоварыОстатки
	|ГДЕ
	|	ТоварыОстатки.КоличествоОстаток < 0"
	);
	
	МоментВремениКонтроля = Дата(1, 1, 1);
	Если Режим = РежимПроведенияДокумента.Неоперативный Тогда 
		МоментВремениКонтроля = Новый Граница(МоментВремени(), ВидГраницы.Включая);
	КонецЕсли;
	
	ЗапросОстатков.УстановитьПараметр("МоментВремени", МоментВремениКонтроля);
	ЗапросОстатков.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = ЗапросОстатков.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Отказ = Истина;
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("На складе недостаточно товара %1. Не хватает: %2.",
				Выборка.ТоварПредставление,
				-Выборка.КоличествоОстаток);
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры